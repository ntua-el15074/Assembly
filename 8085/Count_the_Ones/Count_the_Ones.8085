START:
	IN 10H              ;απενεργοποιούμε την προστασία μνήμης
	MVI A,00H		    ;Αρχικοποιούμε το Α με την πρώτη τιμή που θα αποθηκεύσουμε
	LXI H,0900H		    ;Αρχικοποιούμε την μνήμη με την πρώτη διεύθυνση μνήμης που
                        ;που θα αποθηκευτεί ο πρώτος αριθμός
SAVE:
	MOV M,A		        ;βάζουμε το Α στην μνήμη 
	INR A			    ;Προσθέτουμε 1 στο Α
	INX H			    ;Βάζουμε τη μνήμη να δείχνει στην επόμενη θέση
	CPI 7FH		        ;Αν ο Α είναι ίσος 127
	JNZ SAVE		    ;Αν δεν είναι συνεχίζουμε να αποθηκεύουμε


	MVI B,00H		    ;Αρχικοποιούμε το Β με 0, το οποίο θα χρησιμοποιήσουμε σαν
                        ;counter για τις μονάδες
LOAD_NEXT:
	MOV A,M		        ;Από την τελευταία θέση μνήμης που είμασταν βάζουμε την τιμή
                        ;αυτή στον Α
	MVI D,09H		    ;Αρχικοποιούμε το D με τον αριθμό των επαναλήψεων, λόγω της 
                        ;τοποθέτησης των ελέγχων θα πρέπει να είναι ίσος με 9 αντί για 8
CYCLE_THROUGH:		
	DCR D			    ;Μειώνουμε το D κατά 1
	JZ NEXT		        ;Αν είναι μηδέν τότε πάμε στον επόμενο αριθμό
	RRC			        ;Κάνουμε μία δεξιά περιστροφή για να ελέγχουμε αν υπάρχει μόναδα
                        ;σε εκείνη τη θέση
	JNC CYCLE_THROUGH	;αν δεν υπάρχει συνεχίζουμε
	INR B			    ;αν υπάρχει αυξάνουμε τον counter κατά 1
	JMP CYCLE_THROUGH	;και μετά συνεχίζουμε να ελέγχουμε τα επόμενα
NEXT:
	DCR L			    ;για να πάμε στον επόμενο αριθμό στην μνήμη βάζουμε την μνήμη να
                        ;δείχνει στην προηγούμενη θέση μνήμης
	JNZ LOAD_NEXT		;Η μνήμη έχει αρχικοποιηθεί στη θέση Η = 09 και L = 00, επομένως
                        ;αν το L = 0, φτάσαμε στο τελευταίο ψηφίο, αλλιώς συνεχίζουμε
PRINT_B:
	MOV A,B             ;βάλε τον counter στον A για εκτύπωση
	STA 3000H           ;Άναψε τα LED για να δούμε το B

	MVI B,00H           ;Αρχικοποίησε τον B ως counter = 0
	MVI D,7FH           ;στον D τοποθετούμε τον τελευταίο αριθμό που θα ελέγξουμε
CHECK:
	MOV A,M             ;Από την πρώτη θέση μνήμης που είμασταν βάζουμε την τιμή
	CPI 10H             ;είναι αυτή η τιμή μικρότερη του 10Η
	JC NEXT_C           ;αν είναι πήγαινε στον επόμενο αριθμό στη μνήμη αλλιώς συνέχισε
	CPI 61H             ;είναι αυτή η τιμή μεγαλύτερη ή ίση του 61H
	JNC NEXT_C          ;αν είναι πήγαινε στον επόμενο αριθμό αλλιώς συνέχισε
	INR B               ;πρόσθεσε στον counter 1
NEXT_C:
	INR L               ;πήγαινε στην επόμενη θέση μνήμης
    MOV A,M             ;βάλε το περιεχόμενο της θέσης μνήμης στον Α
	DCR D               ;μείωσε το D κατά ένα για να ξέρουμε πότε θα σταματήσουμε
	JZ END              ;αν το D είναι μηδέν σημαίνει ότι τσεκάραμε όλους τους αριθμούς
	JMP CHECK           ;αλλιώς πάμε να ελέγξουμε
END:
	HLT
END
	


































	
